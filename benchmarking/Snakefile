from typing import Literal
from snakemake.utils import min_version
min_version("6.0")

configfile: "config/config.yaml"
configfile: "config/plots.yaml"

#workflow.use_conda = True

from snakehelp import parameters, result


@parameters
class GenomeBuild:
    genome_build: str = "sacCer3"

@parameters
class Individual:
    genome_build: GenomeBuild
    individual: str = "simulated"


@parameters
class SingleHaplotypeAndChromosomeHifiReads:
    individual: Individual
    depth: float = 1.0
    chromosome: str = "chrIV"
    haplotype: Literal[0, 1] = 0


@parameters
class HifiReads:
    individual: Individual
    dataset_size: Literal["small", "medium", "big"]
    depth: float = 10


@parameters
class RawRealHicReads:
    individual: Individual
    dataset_size: Literal["small", "medium", "big"] = "small"
    hic: Literal["hic"] = "hic"
    n_reads: int = 1000

@parameters
class HiCReads:
    reads: RawRealHicReads
    seed: int = 123


@parameters
class HiCReadsHaplotype:
    reads: HiCReads
    haplotype: Literal[0, 1] = 0


@parameters
class HifiasmResults:
    individual: Individual
    dataset_size: Literal["small", "medium", "big"] = "small"
    depth: float = 10
    n_reads: int = 500
    seed: int = 123


@parameters
class HifiasmResultsWithExtraSplits:
    hifiasm_results: HifiasmResults
    source: Literal["assembled_from_hifi", "not_assembled"] = "not_assembled"
    extra_splits: int = 20
    split_on_n_ns: int = 0
    prob_low_mappability_region: float = 0.0
    mean_low_mappability_size: int = 0
    missing_region_mappability: float = 0.0
    ratio_small_contigs: float = 0.0
    min_contig_size: int = 6000

@parameters
class ScaffoldingResults:
    assembly_graph: HifiasmResultsWithExtraSplits
    scaffolder: Literal["yahs", "salsa2", "haphic", "custom", "bnp_scaffolding_nosplit", "bnp_scaffolding", "bnp_scaffolding_dynamic_heatmaps", "true_scaffolder", "bnp1k", "bnp10k", "bnp1k-iteration"] = "bnp_scaffolding"


@result
class ScaffolderAccuracy:
    scaffolding_results: ScaffoldingResults


@result
class ScaffolderEdgeRecall:
    scaffolding_results: ScaffoldingResults


@result
class ScaffolderEdgePrecision:
    scaffolding_results: ScaffoldingResults

@result
class ScaffolderMissingEdges:
    scaffolding_results: ScaffoldingResults


@parameters
class PhasingResults:
    assembly_graph: HifiasmResultsWithExtraSplits
    phaser: Literal["gfase"]


include: github("bioinf-benchmarking/mapping-benchmarking", "rules/reference_genome.smk", branch="master")
#include: "/home/ivargry/dev/sync/mapping-benchmarking/rules/reference_genome.smk"
#include: github("bioinf-benchmarking/mapping-benchmarking", "rules/read_simulation.smk", branch="master")
include: github("bioinf-benchmarking/mapping-benchmarking", "rules/read_simulation.smk", branch="master")
#include: "/home/ivargry/dev/sync/mapping-benchmarking/rules/read_simulation.smk"
include: github("bioinf-benchmarking/mapping-benchmarking", "rules/mason.smk", branch="master")
#include: "/home/ivargry/dev/sync/mapping-benchmarking/rules/mason.smk"
include: github("bioinf-benchmarking/mapping-benchmarking", "rules/plotting.smk", branch="master")
#include: "/home/ivargry/dev/sync/mapping-benchmarking/rules/plotting.smk"
include: "rules/hifi_simulation.smk"
include: "rules/hic_simulation.smk"
include: "rules/hic_mapping.smk"
include: "rules/hifiasm.smk"
include: "rules/yahs.smk"
include: "rules/gfase.smk"
include: "rules/bnp_scaffolding.smk"
include: "rules/quast.smk"
include: "rules/evaluation.smk"
include: "rules/tests.smk"
include: "rules/real_data.smk"
include: "rules/salsa2.smk"
include: "rules/haphic.smk"

ruleorder: download_reference_with_accession > convert_reference_genome_to_fasta


# downloads every reference except saccer3 which is downloaded from ucsc
rule download_reference_with_accession:
    output:
        "data/{species_name, ((?!sacCer3).)*}/reference.fa"
    params:
        accession = lambda wildcards: config["genomes"][wildcards.species_name]["real"]["assembly_accession"]
    shell:
        """
        wget -O {output}.tmp 'https://www.ebi.ac.uk/ena/browser/api/fasta/{params.accession}?download=true&gzip=false' && 
        python scripts/rename_athalia_rosea_contigs.py {output}.tmp > {output}
        """


"""
Rule that overrides make_haplotype_sequence_for_simulation. Does no simulation of variants,
justs copies the reference sequence.
"""
ruleorder: make_unsimulated_haplotype_sequence > make_haplotype_sequence_for_simulation

rule make_unsimulated_haplotype_sequence:
    input:
        "data/{genome_build}/reference.fa",
    output:
        "data/{genome_build}/real/big/haplotype{haplotype}.fa",
    shell:
        """
        cp {input} {output}
        """

